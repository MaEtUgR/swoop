import USBCConn from "usb-connectors/usb-connectors.ato"
import Power from "generics/interfaces.ato"
import USB2 from "generics/interfaces.ato"
import Pair from "generics/interfaces.ato"
import ButtonPullup from "generics/buttons.ato"
import ButtonSKRPACE010 from "generics/buttons.ato"
import SK6805EC20 from "sk6805-ec20/elec/src/sk6805-ec20.ato"
import AH3772SA7 from "ah3772-sa-7/elec/src/AH372-SA-7.ato"


module SKRPACEPullupBtn from ButtonPullup:
    btn -> ButtonSKRPACE010

module IOs:
    """
    Contains all the IOs of the board:
    - USBC input connector - provides 5V to the rest of the board
    - 3 SK6805EC20 LEDs for status indication (charging, battery level, signal)
    - Buttons for menu and power
    - Hall effect sensor for trigger angle detection
    """
    # top level interfaces
    power3v3 = new Power
    power5v = new Power
    usb2 = new USB2
    led_din = new Pair
    hall_sense = new Pair
    power_btn = new Pair
    menu_btn_1 = new Pair
    menu_btn_2 = new Pair
    menu_btn_3 = new Pair


    # LEDs
    usbc_connector = new USBCConn
    led1 = new SK6805EC20
    led2 = new SK6805EC20
    led3 = new SK6805EC20
    power_button = new SKRPACEPullupBtn
    menu_button_1 = new SKRPACEPullupBtn
    menu_button_2 = new SKRPACEPullupBtn
    menu_button_3 = new SKRPACEPullupBtn
    hall_effect = new AH3772SA7

    # Connect power
    power5v ~ usbc_connector.power
    power3v3 ~ led1.power
    power3v3 ~ led2.power
    power3v3 ~ led3.power
    power3v3 ~ power_button.power
    power3v3 ~ menu_button_1.power
    power3v3 ~ menu_button_2.power
    power3v3 ~ menu_button_3.power
    power3v3 ~ hall_effect.power

    # Connect the LEDs
    led_din ~ led1.din
    led1.dout ~ led2.din
    led2.dout ~ led3.din

    # Connect the buttons
    power_btn ~ power_button.output
    menu_btn_1 ~ menu_button_1.output
    menu_btn_2 ~ menu_button_2.output
    menu_btn_3 ~ menu_button_3.output

    # Connect Devices
    usb2 ~ usbc_connector.usb2
    hall_sense ~ hall_effect.output

